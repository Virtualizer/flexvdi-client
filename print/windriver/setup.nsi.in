!define REDMON_DLL "$SYSDIR\flexVDIprint_redmon.dll"
!define REDMON_NAME "flexVDI Redirection Port"
!define PORT_NAME "flexVDIprint"
!define MONITORS_KEY "SYSTEM\CurrentControlSet\Control\Print\Monitors"
!define PORT_CONFIG_KEY "${MONITORS_KEY}\${REDMON_NAME}\Ports\${PORT_NAME}"
!define PDF_PRINTER_NAME "flexVDI PDF Printer"

!macro installPrintDriver
    SetOutPath "$INSTDIR\printdriver"
    File "@PROJECT_SOURCE_DIR@/print/flexvdips.ppd"
    File "@PROJECT_SOURCE_DIR@/print/windriver/gs9.15/gsdll32.dll"
    File "win32/print/windriver/filter.exe"
    SetOutPath "$INSTDIR"
    ${If} ${RunningX64}
        File "win64/print/windriver/redmon.dll"
    ${Else}
        File "win32/print/windriver/redmon.dll"
    ${EndIf}
    Rename /REBOOTOK "$INSTDIR\redmon.dll" "${REDMON_DLL}"
    DetailPrint "Installing Follow-me Printing driver"
    nsExec::Exec '"$INSTDIR\flexvdi-guest-agent.exe" install-followme-printing "${REDMON_DLL}" "$INSTDIR\printdriver\flexvdips.ppd"'
    Pop $0
    ${If} $0 = 0
        DetailPrint "Follow-me Printing driver successfully installed"
        WriteRegStr HKLM "${PORT_CONFIG_KEY}" "Command" '$INSTDIR\printdriver\filter.exe'
    ${Else}
        MessageBox MB_OK|MB_ICONEXCLAMATION "Follow-me Printing installation failed"
    ${EndIf}
!macroend

!macro uninstallPrintDriver
    nsExec::Exec '"$INSTDIR\flexvdi-guest-agent.exe" uninstall-followme-printing'
    DeleteRegKey HKLM "${PORT_CONFIG_KEY}"
    ${If} $0 = 0
        Delete /REBOOTOK "$SYSDIR\redmon.dll"
    ${Else}
        DetailPrint "ERROR Printer monitor could not be removed: $1"
    ${EndIf}
!macroend
