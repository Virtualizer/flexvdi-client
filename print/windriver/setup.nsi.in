!define REDMON_DLL "$SYSDIR\flexVDIprint_redmon.dll"
!define REDMON_NAME "flexVDI Redirection Port"
!define PORT_NAME "flexVDIprint"
!define MONITORS_KEY "SYSTEM\CurrentControlSet\Control\Print\Monitors"
!define PORT_CONFIG_KEY "${MONITORS_KEY}\${REDMON_NAME}\Ports\${PORT_NAME}"
!define PDF_PRINTER_NAME "flexVDI PDF Printer"

!macro installPrintDriver
    SetOutPath "$INSTDIR\printdriver"
    File "@PROJECT_SOURCE_DIR@/print/flexvdips.ppd"
    File "@PROJECT_SOURCE_DIR@/print/windriver/gs9.15/gsdll32.dll"
    File "win32/print/windriver/filter.exe"
    SetOutPath "$INSTDIR"
    ${If} ${RunningX64}
        File "win64/print/windriver/redmon.dll"
    ${Else}
        File "win32/print/windriver/redmon.dll"
    ${EndIf}
    Rename /REBOOTOK "$INSTDIR\redmon.dll" "${REDMON_DLL}"
    DetailPrint "Installing Follow-me Printing driver"
    nsExec::ExecToStack '"$INSTDIR\flexvdi-guest-agent.exe" install-followme-printing "${REDMON_DLL}" "$INSTDIR\printdriver\flexvdips.ppd"'
    Pop $0
    Pop $1
    ${If} $0 == "error"
        MessageBox MB_OK|MB_ICONEXCLAMATION "Could not execute guest agent"
    ${ElseIf} $0 = 0
        DetailPrint "Follow-me Printing driver successfully installed"
        WriteRegStr HKLM "${PORT_CONFIG_KEY}" "Command" '$INSTDIR\printdriver\filter.exe'
    ${Else}
        MessageBox MB_OK|MB_ICONEXCLAMATION "Follow-me Printing installation failed: $1"
    ${EndIf}
!macroend

!macro uninstallPrintDriver
    nsExec::ExecToStack '"$INSTDIR\flexvdi-guest-agent.exe" uninstall-followme-printing'
    Pop $0
    Pop $1
    ${If} $0 == "error"
        DetailPrint "Could not execute guest agent"
    ${ElseIf} $0 = 0
        Delete /REBOOTOK "${REDMON_DLL}"
    ${Else}
        DetailPrint "ERROR Printer monitor could not be removed: $1"
    ${EndIf}
!macroend
