;
; Copyright Flexible Software Solutions S.L. 2014
;

!include credentials-manager/credential-provider/setup.nsi
!include credentials-manager/gina/setup.nsi
!include print/windriver/setup.nsi
!define UNINSTALL_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}"

Name "${APPNAME} v${APPVERSION}"
Icon @PROJECT_SOURCE_DIR@/images/icon.ico
InstallDirRegKey HKLM "Software\${APPNAME}" "Install_Dir"
RequestExecutionLevel admin
ShowInstDetails show
ShowUninstDetails show
SetOverwrite try
SetPluginUnload alwaysoff

; Version Information
VIProductVersion "${APPVERSION}.0"
VIAddVersionKey "ProductName" "${APPNAME}"
VIAddVersionKey "CompanyName" "Flexible Software Solutions S.L."
VIAddVersionKey "LegalCopyright" "Copyright Flexible Software Solutions S.L. 2014"
VIAddVersionKey "FileDescription" "${APPNAME}"
VIAddVersionKey "FileVersion" "${APPVERSION}"
VIAddVersionKey "ProductVersion" "${APPVERSION}"

; Pages
Page components
Page instfiles
UninstPage instfiles


Function .onInit
    ${IfNot} ${AtLeastWinXP}
        MessageBox MB_OK "XP and above required"
        Quit
    ${EndIf}
    ${If} ${RunningX64}
        StrCpy $INSTDIR "$PROGRAMFILES64\flexVDI"
    ${Else}
        StrCpy $INSTDIR "$PROGRAMFILES32\flexVDI"
    ${EndIf}
FunctionEnd


Section "!flexVDI guest agent"
    SectionIn RO
    ${DisableX64FSRedirection}
    SetOutPath $INSTDIR
    SetRegView 64
    SetAutoClose true

    ; Write the uninstall keys for Windows
    WriteRegStr HKLM "${UNINSTALL_KEY}"   "DisplayName" "${APPNAME}"
    WriteRegStr HKLM "${UNINSTALL_KEY}"   "DisplayIcon" "$INSTDIR\flexvdi-guest-agent.exe"
    WriteRegStr HKLM "${UNINSTALL_KEY}"   "DisplayVersion" "@FLEXVDI_VERSION@"
    WriteRegStr HKLM "${UNINSTALL_KEY}"   "InstallLocation" "$INSTDIR"
    WriteRegStr HKLM "${UNINSTALL_KEY}"   "Publisher" "Flexible Software Solutions S.L."
    WriteRegStr HKLM "${UNINSTALL_KEY}"   "UninstallString" '"$INSTDIR\uninstall.exe"'
    WriteRegDWORD HKLM "${UNINSTALL_KEY}" "NoModify" 1
    WriteRegDWORD HKLM "${UNINSTALL_KEY}" "NoRepair" 1
    WriteUninstaller "uninstall.exe"

    ; Agent
    nsExec::Exec '"$INSTDIR\flexvdi-guest-agent.exe" uninstall'
    ${If} ${RunningX64}
        File "../win64/flexvdi-guest-agent.exe"
    ${Else}
        File "../win32/flexvdi-guest-agent.exe"
    ${EndIf}
    nsExec::ExecToStack '"$INSTDIR\flexvdi-guest-agent.exe" install'
    Pop $0
    ${If} $0 = 0
        DetailPrint "Guest agent installed successfully"
    ${Else}
        Pop $0
        MessageBox MB_OK|MB_ICONEXCLAMATION "Guest agent failed to install: $0"
    ${EndIf}
SectionEnd


Section "!SPICE guest tools"
    SectionIn RO
    SetOutPath $INSTDIR
    File "@SPICE_GUEST_TOOLS@"
    nsExec::Exec '"$INSTDIR\@SPICE_GUEST_TOOLS@"'
    Delete "$INSTDIR\@SPICE_GUEST_TOOLS@"
SectionEnd


Section "Single Sign-On plugin"
    ${DisableX64FSRedirection}
    SetRegView 64
    SetOutPath $INSTDIR
    ${If} ${IsWinXP}
        !insertmacro installGina
    ${Else}
        !insertmacro installCredProvider
    ${EndIf}
SectionEnd


Section "Follow-me Printing driver"
    ${DisableX64FSRedirection}
    SetRegView 64
    !insertmacro installPrintDriver
SectionEnd


Function .onInstSuccess
    ; Compute installed size
    ${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
    IntFmt $0 "0x%08X" $0
    WriteRegDWORD HKLM "${UNINSTALL_KEY}" "EstimatedSize" "$0"
    IfRebootFlag 0 noreboot
        MessageBox MB_YESNO|MB_ICONQUESTION "The system must reboot, do you wish to do it now?" IDNO noreboot
            Reboot
    noreboot:
FunctionEnd


Section "Uninstall"
    SetAutoClose true
    ${DisableX64FSRedirection}

    ${If} ${IsWinXP}
        !insertmacro uninstallGina
    ${Else}
        !insertmacro uninstallCredProvider
    ${EndIf}

    !insertmacro uninstallPrintDriver

    DeleteRegKey HKLM "${UNINSTALL_KEY}"
    nsExec::Exec '"$INSTDIR\flexvdi-guest-agent.exe" uninstall'
    Pop $0
    ${If} $0 = 0
        DetailPrint "Guest agent service removed"
        RMDir /r /REBOOTOK "$INSTDIR"
    ${Else}
        Pop $0
        MessageBox MB_OK|MB_ICONEXCLAMATION "Guest agent failed to uninstall: $0"
    ${EndIf}
SectionEnd
